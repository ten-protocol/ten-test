set terminal pdf enhanced size 18cm, 18cm
set output ARG2."_all_clients.pdf"
set multiplot layout 2,2 rowsfirst title ARG2.": ".ARG1." (All clients)" noenhanced scale 0.95,0.95
set grid
set lmargin 10
set bmargin 3
set key font 'Courier,10'
set key right top
set yrange [*<0:*]
set xtics font "Courier,11"
set ytics nomirror font "Courier,11"
set y2tics nomirror font "Courier,11"
set label "{/Courier:Bold=13 Branch}: ".ARG3 left at screen 0.59, screen 0.925
set label "{/Courier:Bold=13 Date}: ".ARG4 left at screen 0.59, screen 0.900
set label "{/Courier:Bold=13 Environment}: ".ARG5 left at screen 0.59, screen 0.875

#plot 1
set size 0.5, 0.45
set origin 0.0, 0.50
set style fill solid 0.5
set title "{/Arial:Bold=13 Avg, Mode, 99th Percentile Latency}"
set xlabel "Clients"  font "Courier,12"
set ylabel "Latency (ms)" font "Courier,12" offset 0,0
set y2label "99th Percentile Latency (ms)" font "Courier,12" offset 1,0
plot "results.log" using 1:3 with linespoints title "Average" axis x1y1, \
     "results.log" using 1:4 with linespoints title "Mode" axis x1y1, \
     "results.log" using 1:5 with linespoints title "99th Percentile" lt 8 axis x1y2

#plot 2
unset yrange
unset y2label
set size 0.5, 0.45
set origin 0.0, 0.05
set style fill solid 0.5
set title "{/Arial:Bold=13 Throughput}"
set xlabel "Clients"  font "Courier,12"
set ylabel "Throughput (requests/sec)" font "Courier,12" offset 1,0
plot "results.log" using 1:2 with linespoints title "Throughput"