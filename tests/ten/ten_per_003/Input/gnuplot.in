set terminal pdf enhanced size 18cm, 18cm
set output ARG2.".pdf"
set multiplot layout 2,2 rowsfirst title ARG2.": ".ARG1 noenhanced scale 0.95,0.95
set grid
set lmargin 10
set bmargin 3
set key font 'Courier,10'
set key right top
set xtics font "Courier,11"
set ytics font "Courier,11"
set xrange [0: ARG7]
set style fill solid 0.5
set label "{/Courier:Bold=13 Branch}: ".ARG3 left at screen 0.59, screen 0.925
set label "{/Courier:Bold=13 Date}: ".ARG4 left at screen 0.59, screen 0.900
set label "{/Courier:Bold=13 Environment}: ".ARG5 left at screen 0.59, screen 0.875
set label "{/Courier:Bold=13 Transactions}: ".ARG6 left at screen 0.59, screen 0.850
set label "{/Courier:Bold=13 Duration}: ".ARG7 left at screen 0.59, screen 0.825
set label "{/Courier:Bold=13 Average}: ".ARG8 left at screen 0.59, screen 0.800
stats "client_one.bin" using 1:2 name "CLIENT1"
stats "client_two.bin" using 1:2 name "CLIENT2"
stats "clients.bin" using 1:2 name "CLIENTS"
stats "clients_receipt_times.bin" using 1:2 name "CLIENTS_RECEIPTS"

#plot 1
set size 0.5, 0.25
set origin 0.0, 0.70
unset xtics
set yr [0: ((CLIENT1_max_y)*1.2)]
set title "{/Arial:Bold=13 Per Client Binned Transactions }"
set ylabel "Transactions" font "Courier,12" offset 1,0
plot "client_one.bin" using 1:2 with boxes title "Client One" lc 2

set size 0.5, 0.25
set origin 0.0, 0.50
set xtics
set yr [0: ((CLIENT2_max_y)*1.2)]
unset title
set xlabel "Time (seconds)" font "Courier,12"
set ylabel "Transactions" font "Courier,12" offset 1,0
plot "client_two.bin" using 1:2 with boxes title "Client Two" lc 3

#plot 2
set size 0.5, 0.45
set origin 0.0, 0.04
set yr [0: ((CLIENTS_max_y)*1.2)]
set title "{/Arial:Bold=13 Cumulative Binned Transactions }"
set style line 1 lc rgb 'gray30' lt 1 lw 1.2
set style fill solid 0.3 border rgb 'grey30'
set xlabel "Time (seconds)" font "Courier,12"
set ylabel "Transactions" font "Courier,12" offset 1,0
plot "clients.bin" using 1:2 with boxes ls 1 title "Binned", "" using 1:(CLIENTS_mean_y) with lines title "Average"

#plot 3
set size 0.5, 0.45
set origin 0.5, 0.05
set yr [0: ((CLIENTS_RECEIPTS_max_y)*1.2)]
set xr [0: ((CLIENTS_RECEIPTS_max_x)*1.1)]
set boxwidth 0.05
set title "{/Arial:Bold=13 Cumulative TX Receipt Waits }"
set style line 1 lc rgb 'gray30' lt 1 lw 1.2
set style fill solid 0.3 border rgb 'grey30'
set xlabel "Time (seconds)" font "Courier,12"
set ylabel "Number" font "Courier,12" offset 1,0
plot "clients_receipt_times.bin" using 1:2 with boxes ls 1 title "Binned", "" using 1:(CLIENTS_RECEIPTS_mean_y) with lines title "Average"
