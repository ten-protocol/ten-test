set terminal pdf enhanced size 18cm, 18cm
set output ARG2.".pdf"
set multiplot layout 2,2 rowsfirst title ARG2.": ".ARG1." (4 clients)" noenhanced scale 0.95,0.95
set grid
set lmargin 10
set bmargin 3
set key font 'Courier,10'
set key right top
set xtics font "Courier,11"
set ytics font "Courier,11"
set label "{/Courier:Bold=13 Branch}: ".ARG3 left at screen 0.59, screen 0.925
set label "{/Courier:Bold=13 Date}: ".ARG4 left at screen 0.59, screen 0.900
set label "{/Courier:Bold=13 Environment}: ".ARG5 left at screen 0.59, screen 0.875
set label "{/Courier:Bold=13 Throughput}: ".ARG6." txs/sec" left at screen 0.59, screen 0.850
stats "clients_4/binned_throughput_all.log" using 1:2 nooutput name "A"
stats "clients_5/binned_throughput_all.log" using 1:2 nooutput name "B"

#plot 1
set size 0.5, 0.45
set origin 0.0, 0.50
set style fill solid 0.5
set title "{/Arial:Bold=13 Throughput overlapping client txns}"
set xlabel "Num Clients" font "Courier,12"
set ylabel "Throughput (txs/sec)" font "Courier,12" offset 1,0
plot "results.log" using 1:2 with linespoints pt 4 title "Throughput"

#plot 2
set yr [0:1.3*(A_max_y)]
set xr [0:1.1*(A_max_x)]
set size 0.5, 0.45
set origin 0.0, 0.05
set boxwidth 1
set style fill solid border -1
set title "{/Arial:Bold=13 Binned Throughput (4 clients)}"
set xlabel "Time (s)"  font "Courier,12"
set ylabel "Rate (txs/sec)" font "Courier,12" offset 1,0
plot "clients_4/binned_throughput_steady.log" using 1:2 with boxes fill solid 0.8 lc 1 fc rgb "dark-grey" title "Binned steady throughput", \
     "clients_4/binned_throughput_ramp.log" using 1:2 with boxes fill solid 0.25 lc 1 fc rgb "grey" title "Binned ramp throughput", \
     "clients_4/binned_throughput_all.log" using 1:2 smooth bezier w l lw 2 lc 2 title "Smoothed throughput", \
     "clients_4/binned_throughput_steady.log" using 1:3 w l lw 4 lc 7 title "Avg throughput"

#plot 3
set yr [0:1.3*(B_max_y)]
set xr [0:1.1*(B_max_x)]
set size 0.5, 0.45
set origin 0.5, 0.05
set boxwidth 1
set style fill solid border -1
set title "{/Arial:Bold=13 Binned Throughput (5 clients)}"
set xlabel "Time (s)"  font "Courier,12"
set ylabel "Rate (txs/sec)" font "Courier,12" offset 1,0
plot "clients_5/binned_throughput_steady.log" using 1:2 with boxes fill solid 0.8 lc 1 fc rgb "dark-grey" title "Binned steady throughput", \
     "clients_5/binned_throughput_ramp.log" using 1:2 with boxes fill solid 0.25 lc 1 fc rgb "grey" title "Binned ramp throughput", \
     "clients_5/binned_throughput_all.log" using 1:2 smooth bezier w l lw 2 lc 2 title "Smoothed throughput", \
     "clients_5/binned_throughput_steady.log" using 1:3 w l lw 4 lc 7 title "Avg throughput"