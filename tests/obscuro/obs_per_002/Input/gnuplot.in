set terminal pdf enhanced size 18cm, 18cm
set output ARG2.".pdf"
set multiplot layout 2,2 rowsfirst title ARG2.": ".ARG1 noenhanced scale 0.95,0.95
set grid
set lmargin 10
set bmargin 3
set key font 'Courier,10'
set key right top
set xtics font "Courier,11"
set ytics font "Courier,11"
set xrange [0: ARG7]
set label "{/Courier:Bold=13 Branch}: ".ARG3 left at screen 0.59, screen 0.925
set label "{/Courier:Bold=13 Date}: ".ARG4 left at screen 0.59, screen 0.900
set label "{/Courier:Bold=13 Environment}: ".ARG5 left at screen 0.59, screen 0.875
set label "{/Courier:Bold=13 Transactions}: ".ARG6 left at screen 0.59, screen 0.850
set label "{/Courier:Bold=13 Duration}: ".ARG7 left at screen 0.59, screen 0.825
set label "{/Courier:Bold=13 Average}: ".ARG8 left at screen 0.59, screen 0.800
stats "clients.bin" using 1:2

#plot 1
set size 0.5, 0.25
set origin 0.0, 0.70
set title "{/Arial:Bold=13 Per Client Binned Transactions }"
unset xtics
set ylabel "Transactions" font "Courier,12" offset 1,0
plot "client_one.bin" using 1:2 with boxes title "Client One" lc 2

set size 0.5, 0.25
set origin 0.0, 0.50
unset title
set xtics
set xlabel "Time (seconds)" font "Courier,12"
set ylabel "Transactions" font "Courier,12" offset 1,0
plot "client_two.bin" using 1:2 with boxes title "Client Two" lc 3

#plot 2
set size 0.5, 0.45
set origin 0.0, 0.04
set title "{/Arial:Bold=13 Cummulative Binned Transactions }"
set xlabel "Time (seconds)" font "Courier,12"
set ylabel "Transactions" font "Courier,12" offset 1,0
plot "clients.bin" using 1:2 with boxes title "Binned", "" using 1:(STATS_mean_y) with lines title "Average"
